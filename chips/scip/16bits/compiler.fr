\ (c) 2004 Johns Hopkins University / Applied Physics Laboratory
hex

\ Promote instruction templates for use by compiler
	<small-lit>	define <small-lit>
	<small+lit>	define <small+lit>
	<lo-lit>	define <lo-lit>
	<lo-off>	define <lo-off>
	<small-off>	define <small-off>
	<small+off>	define <small+off>

nohead
: *inline*	\ ( cfa --- ) copy the routine indicated by cfa inline.
 ['] return				\ cfa return-cfa
 2dup =
 if drop @ ,				\ if return, compile return opcode
 else @ swap				\ return-code cfa
    begin
      2dup @ <> while			\ while not return instruction
      dup @ , 2 +
 repeat 2drop
 then ;

nohead
: *callop*	\ ( xt -- x ) Return instruction that calls xt.
   1 rshift ;
: compile,	\ ( xt -- ) Compile call.  Use shortest form possible.
   1 rshift , ;

nohead
: *code*	\ ( cfa --- ) if code pointed to by cfa is to be brought
		\ in line, copy it into the dictionary.  Otherwise, compile
		\ the cfa.
 dup >name c@ 80 and			\ inline?
 if *inline*
 else compile,				\ otherwise, call it
 then ;

metaextend
forth : postpone
 [forth] bl word count ['] target >body @ search-wordlist
 if >body dup @ swap cell+ @
    sel 
      << callable	    ==> [meta] literal [target] compile, [forth] >>
      << inline-from-target ==> [meta] literal [target] *inline* [forth] >>
      << inline-from-host   ==> drop
				s" can't postpone" cross-error >>
      << immediate-target   ==> [host] *call* >>
    endsel
 else [forth] s" ?" cross-error
 then ;
hostinterp

nohead
: *literal*	\ ( n -- ) Compile a literal instruction.
   dup 20 u< if				\ if 0 - 31
      4 lshift <small+lit> or ,		\ compile small positive literal
   else dup ffffffdf u> if		\ if 0xffffffe0 - 0xffffffff or -32 - -1
      invert 4 lshift <small-lit> or ,	\ compile small negative literal
   else					\ else, small literal
      <lo-lit> , ,			\ compile it
   then then ;

nohead
: *offset*	\ ( n -- ) Compile a literal offset instruction.
   dup 20 u< if				\ if 0 - 31
      4 lshift <small+off> or ,		\ compile small positive literal offset
   else dup ffffffe0 u> if		\ if 0xffffffe1 - 0xffffffff or -31 - -1
      negate 4 lshift <small-off> or ,	\ compile small negative literal offset
   else					\ else, small literal
      <lo-off> , ,			\ compile it
   then then ;

noheads	\ this stuff is only used by the compiler
: *branch-dest*	\ ( src dst -- )
   dup 3ff and 2/ >r			\ save destination offset
   fc00 and				\ s: src dstblk
   dup 0= if				\ if branch to page 0
      drop 400 ( page0)			\ s: src blkpattern
   else
      over 2 + fc00 and			\ s: src dstblk srcblk
      2dup <>				\ if block crossing
      if				\ make sure not too far to branch
	 - dup abs 400 u> if ." branch too far" then
	 0< if 600 ( back) else 200 ( fwd) then \ select previous or next block
      else 2drop 0			\ current block
      then				\ s: src blkpattern
   then
   r> or				\ s: src dst-blkpatt/offset
   over @ f800 and or			\ assemble new instruction
   swap ! ;

: *branch-unpack* \ ( src -- dst ) Extract branch destination address from
		\ a branch instruction.
   dup @ dup 1ff and 2* >r		\ record dst offset
   600 and				\ src dsttype
   dup 400 = if 2drop 0
   else
      swap 2 + fc00 and swap		\ s: srcblk dsttype
      2* dup 800 and if fffff000 or then \ compute 
      +
   then
   r> or ;

\ does> support
: does-parent
   r> *callop* latest name> ! ;

: does-child
   r> || ; inline-keepbody

heads
