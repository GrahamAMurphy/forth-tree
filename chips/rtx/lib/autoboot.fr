\ (c) 1998 Johns Hopkins University / Applied Physics Laboratory
\ Automatically boot from EEPROM.
\ This is intended to be a component for a boot ROM ...
hex

\ Requires the following words:
\ eeprom-prog-pg#	\ ( -- page ) Returns page of source program.
\ eeprom-prog-addr	\ ( -- addr ) Returns address of source program header.

structure prog-head-struct	\ header of program
   cell: .prog-page		\ page in which to load program
   cell: .prog-dst		\ destination address in page
   cell: .prog-exec		\ program entry point
   cell: .prog-length		\ program length in words (including header)
   cell: .prog-cksum		\ 16 bit 2's complement checksum
\ program begins here ...
   cell: .prog-start		\ hack: avoid prog-head-struct
endstructure

: eeprom-good?	\ ( -- t/f ) return true if EEPROM checksum is good.
		\ The checksum is the 16 bit 2's complement checksum of the
		\ the header and program.  The header includes a value,
		\ (the checksum) to make this computation produce zero.
   eeprom-prog-pg# set-page far		\ select eeprom page
   0 eeprom-prog-addr			\ s: initial-sum source-addr
   dup .prog-length @ 0			\ file length in words
   do swap over @ + swap cell+ loop drop \ accumulate checksum
   0= ;

: eeprom-boot	\ ( -- ) Try to boot EEPROM program.  The destination
		\ RAM page is stored in the EEPROM; if it is the same
		\ as the current code page, the copy is not done.
   eeprom-prog-pg# set-page  far	\ access EEPROM page; all data is remote
   eeprom-prog-addr .prog-page @	\ get destination page
   dup cpr@ <> if			\ refuse to overwrite boot program
      eeprom-prog-addr .prog-start	\ s: dst page, src addr,
      eeprom-prog-addr .prog-dst @	\    dest addr
      eeprom-prog-addr .prog-length @	\ length to copy in words
      for
	 >r eeprom-prog-pg# set-page @+ swap
	 2pick set-page r> !+
      next 2drop
      eeprom-prog-pg# set-page  eeprom-prog-addr .prog-exec @
      swap cpr! execute			\ jump to loaded program
   else drop
   then ;

: autoboot	\ ( -- ) If EEPROM, run default pgm.  Will return on failure
		\ of if booted program returns.
   eeprom-good?				\ if eeprom is ok
   if eeprom-boot then ;		\ copy and run the program
